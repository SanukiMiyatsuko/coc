BNF記法を用いる。
空白はあってもなくても構わない。
正規表現のメタ文字も利用する。

予約語：
def
var
Prop
Type
fun
forall
exist
let
in

文字列：
<ident> ::= [a-zA-Z_][\w']*
<blanks> ::= \s+
項：
<sort> ::= Prop | Type
<variable> ::= <ident>
<open_binder> ::= <ident>+:<type>
<closed_binder> ::= \(<open_binder>\)
<binder> ::= <open_binder> | <closed_binder>+
<lam> ::= fun<binder>=><term>
<pi> ::= forall<binder>,<type>
<pair> ::= \<<term>,<term>\>
<sigma> ::= exist<binder>,<type>
<let> ::= let<ident><closed_binder>*:<type>:=<term>in<term>
<atom> ::= <sort> | <variable> | \(<term>\) | <pair>
<proj> ::= <atom>(\.[01])*
<app> ::= <app><proj> | <proj>
<prod> ::= <prod>&<app> | <app>
<array> ::= <prod>-><type> | <prod>
<term> ::= <lam> | <pi> | <sigma> | <let> | <array>
<type> ::= <term>
トップレベル：
<line_comment> ::= --[^\n]*(?:\n|$)
<block_comment> ::= {-[\s\S]*?-}
<global_def> ::= def<ident><closed_binder>*:<type>:=<term>;
<global_var> ::= var<ident><closed_binder>*:<type>;
<program> ::= (<global_def> | <global_var>)* EOF